{*******************************************************}
{                    API PDV - JSON                     }
{                      ES Sistemas                      }
{          Início do projeto 03/04/2024 16:03           }
{                 www.bemoreweb.com.br                  }
{                     (17)98169-5336                    }
{                        2003/2024                      }
{         Analista desenvolvedor (Eleandro Silva)       }
{*******************************************************}
unit Model.DAO.Imp.Email.Empresa;

interface

uses
  Data.DB,

  System.SysUtils,

  Uteis,
  Uteis.Tratar.Mensagens,

  Model.DAO.Email.Empresa.Interfaces,
  Model.Entidade.Email.Empresa.Interfaces,
  Model.Conexao.Firedac.Interfaces,
  Model.Conexao.Query.Interfaces,
  Model.Entidade.Imp.Email.Empresa,
  Model.Conexao.Firedac.MySQL.Imp,
  Model.Conexao.Query.Imp;

type
  TDAOEmailEmpresa = class(TInterfacedObject, iDAOEmailEmpresa)
    private
      FEmailEmpresa : iEntidadeEmailEmpresa<iDAOEmailEmpresa>;
      FConexao      : iConexao;
      FQuery        : iQuery;
      FUteis        : TUteis;
      FMSG          : TMensagens;
      FDataSet      : TDataSet;

      FKey     : String;
      FValue   : String;

      const
        FSQL=('select '+
              'ee.id, '+
              'ee.idempresa, '+
              'ee.email, '+
              'ee.tipoemail, '+
              'ee.ativo '
              );
        FCount=(','+
                '('+
                'select '+
                'count(*) '+
                'from emailempresa ee1 '+
                'where ee1.idempresa=ee.idempresa '+
                ') as quantidaderegistro '
               );
        FFrom=('from emailempresa ee ');

      procedure FiltroKey;
      function  FiltroValue(Value : String) : String;
      function  LoopRegistro(Value: Integer): Integer;
    public
      constructor Create;
      destructor Destroy; override;
      class function New : iDAOEMailEmpresa;

      function DataSet(DataSource : TDataSource) : iDAOEmailEmpresa; overload;
      function DataSet                           : TDataSet;         overload;
      function GetAll                            : iDAOEmailEmpresa;
      function GetbyId(Id : Variant)             : iDAOEmailEmpresa;
      function GetbyParams                       : iDAOEmailEmpresa;
      function Post                              : iDAOEmailEmpresa;
      function Put                               : iDAOEmailEmpresa;
      function Delete                            : iDAOEmailEmpresa;
      function QuantidadeRegistro                : Integer;

      function This : iEntidadeEmailEmpresa<iDAOEmailEmpresa>;
  end;

implementation

{ TDAOEmailEmpresa }

constructor TDAOEmailEmpresa.Create;
begin
  FEmailEmpresa := TEntidadeEmailEmpresa<iDAOEmailEmpresa>.New(Self);
  FConexao      := TModelConexaoFiredacMySQL.New;
  FQuery        := TQuery.New;
  FUteis        := TUteis.Create;
  FMSG          := TMensagens.Create;
end;

destructor TDAOEmailEmpresa.Destroy;
begin
  FreeAndNil(FUteis);
  FreeAndNil(FMSG);
  inherited;
end;

class function TDAOEmailEmpresa.New: iDAOEMailEmpresa;
begin
  Result := Self.Create;
end;

procedure TDAOEmailEmpresa.FiltroKey;
begin
  if FEmailEmpresa.IdEmpresa > 0 then FKey := 'ee.idempresa' else
                                      FKey := 'ee.email';
  FValue := FiltroValue(FKey);
end;

function TDAOEmailEmpresa.FiltroValue(Value: String): String;
begin
  Result := '';
  if Value ='ee.idempresa' then
    Result := IntToStr(FEmailEmpresa.IdEmpresa) else Result := FEmailEmpresa.Email;
end;

function TDAOEmailEmpresa.DataSet(DataSource: TDataSource): iDAOEmailEmpresa;
begin
  Result := Self;
  if not Assigned(FDataset) then
    DataSource.DataSet := FQuery.DataSet
  else
    DataSource.DataSet := FDataSet;
end;

function TDAOEmailEmpresa.DataSet: TDataSet;
begin
  Result := FDataSet;
end;

function TDAOEmailEmpresa.GetAll: iDAOEmailEmpresa;
begin
  Result := Self;
  try
    try
      FDataSet := FQuery
                    .SQL(FSQL+FCount+FFrom)
                    .Open
                  .DataSet;
    except
     raise Exception.Create(FMSG.MSGerroGet);
    end;
  finally
    if not FDataSet.IsEmpty then
    begin
      FEmailEmpresa.Id(FDataSet.FieldByName('id').AsInteger);
      QuantidadeRegistro;
    end else FEmailEmpresa.Id(0);
  end;
end;

function TDAOEmailEmpresa.GetbyId(Id: Variant): iDAOEmailEmpresa;
begin
  Result := Self;
  try
    try
      FDataSet := FQuery
                    .SQL(FSQL+FFrom)
                    .Add('where ee.id=:id')
                    .Params('Id', Id)
                    .Open
                  .DataSet;
    except
      raise Exception.Create(FMSG.MSGerroGet);
    end;
  finally
    if not FDataSet.IsEmpty then
      FEmailEmpresa.Id(FDataSet.FieldByName('id').AsInteger) else FEmailEmpresa.Id(0);
  end;
end;

function TDAOEmailEmpresa.GetbyParams: iDAOEmailEmpresa;
begin
  Result := Self;
  FiltroKey;
  try
   try
     FDataSet := FQuery
                   .SQL(FSQL+FCount+FFrom+' where ' + FUteis.Pesquisar(FKey, ';' + FValue))
                   .Open
                 .DataSet;
   except
     raise exception.Create(FMSG.MSGerroGet);
   end;
  finally
   if not FDataSet.IsEmpty then
   begin
     FEmailEmpresa.Id(FDataSet.FieldByName('id').AsInteger);
     QuantidadeRegistro;
   end else FEmailEmpresa.Id(0);
  end;
end;

function TDAOEmailEmpresa.Post: iDAOEmailEmpresa;
const
  LSQL=('insert into emailempresa( '+
                                 'idempresa, ' +
                                 'email, '+
                                 'tipoemail, '+
                                 'ativo '+
                                 ') '+
                                 ' values '+
                                 '('+
                                 ':idempresa, ' +
                                 ':email, '+
                                 ':tipoemail, '+
                                 ':ativo '+
                                 ')'
       );
begin
  Result := Self;
  FConexao.StartTransaction;
  try
    try
      FQuery
        .SQL(LSQL)
          .Params('idempresa' , FEmailEmpresa.IdEmpresa)
          .Params('email'     , FEmailEmpresa.Email)
          .Params('tipoemail' , FEmailEmpresa.TipoEmail)
          .Params('ativo'     , FEmailEmpresa.Ativo)
          .ExecSQL;
    except
      FConexao.Rollback;
      raise Exception.Create(FMSG.MSGerroPost);
    end;
  finally
    FConexao.Commit;
    FDataSet := FQuery
                    .SQL('select LAST_INSERT_ID () as id')
                    .Open
                    .DataSet;
    FEmailEmpresa.Id(FDataSet.FieldByName('id').AsInteger);
  end;
end;

function TDAOEmailEmpresa.Put: iDAOEmailEmpresa;
const
  LSQL=('update emailempresa set '+
                               'idempresa =:idempresa, '+
                               'email     =:email, '+
                               'tipoemail =:tipoemail, '+
                               'ativo     =:ativo '+
                               'where id=:id '
       );
begin
  Result := Self;

  FConexao.StartTransaction;
  try
    try
      FQuery
        .SQL(LSQL)
          .Params('id'        , FEmailEmpresa.Id)
          .Params('idempresa' , FEmailEmpresa.IdEmpresa)
          .Params('email'     , FEmailEmpresa.Email)
          .Params('tipoemail' , FEmailEmpresa.TipoEmail)
          .Params('ativo'     , FEmailEmpresa.Ativo)
        .ExecSQL;
    except
      FConexao.Rollback;
      raise Exception.Create(FMSG.MSGerroPut);
    end;
  finally
    FConexao.Commit;
  end;
end;

function TDAOEmailEmpresa.Delete: iDAOEmailEmpresa;
const
  LSQL=('delete from emailempresa where id=:id ');
begin
  Result := self;
  FConexao.StartTransaction;
  try
    try
      FQuery.SQL(LSQL)
               .Params('id', FEmailEmpresa.Id)
            .ExecSQL;
    except
      FConexao.Rollback;
      raise Exception.Create(FMSG.MSGerroDelete);
    end;
  finally
    FConexao.Commit;
  end;
end;

function TDAOEmailEmpresa.LoopRegistro(Value: Integer): Integer;
begin
  FDataSet.First;
  try
    while not FDataSet.Eof do
    begin
      Value := Value + 1;
      FDataSet.Next;
    end;
  finally
    Result := Value;
  end;
end;

function TDAOEmailEmpresa.QuantidadeRegistro: Integer;
begin
  Result := LoopRegistro(0);
end;

function TDAOEmailEmpresa.This: iEntidadeEmailEmpresa<iDAOEmailEmpresa>;
begin
  Result := FEmailEmpresa;
end;

end.
